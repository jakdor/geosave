/*
 * GeoSave - app for easy sharing and collaborating on GPS related data
 * Copyright (C) 2018  Jakub Dorda
 *
 * Software under GPLv3 licence - full copyright notice available at:
 * https://github.com/jakdor/geosave/blob/master/README.md
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'io.fabric'

project.ext {
    androidxVersion = "1.0.0"
    androidxAppcompatVersion = "1.0.2"
    archVersion = "2.0.0"
    constraintLayoutVersion = "2.0.0-alpha2"
    firebaseCoreVersion = "16.0.5"
    firebaseFirestoreVersion = "17.1.0"
    firebaseStorageVersion = "16.0.5"
    crashlyticsVersion = "2.9.6"
    firebaseUIVersion = "4.1.0"
    daggerVersion = "2.13"
    rxKotlinVersion = "2.2.0"
    rxAndroidVersion = "2.0.1"
    timberVersion = "4.7.0"
    gmsBaseVersion = "16.0.1"
    gmsBasementVersion = "16.1.0"
    gmsVersion = "16.0.0"
    glideVersion = "4.8.0"
    retrofitVersion = "2.3.0"
    okHttpVersion = "3.9.1"
    fabMenuVersion = "1.10.1"
    easyImageVersion = "2.1.0"
    compressorVersion = "2.1.0"
    leakCanaryVersion = "1.6.1"

    junitVersion = "4.12"
    mockitoVersion = "2.13.0"
    mockitoKotlinVersion = "1.5.0"
    robolectricVersion = "3.6.1"
    robolectricShadowsVersion = "3.3.2"
}

android {
    namespace 'com.jakdor.geosave'
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.jakdor.geosave"
        minSdkVersion 21
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [maps: getApiKey("maps.key")]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            versionNameSuffix "-dev"
        }
        production {
            dimension "version"
            versionNameSuffix "-production"
        }
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues true
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //APP

    //Support libs
    implementation "androidx.appcompat:appcompat:$project.androidxAppcompatVersion"
    implementation "androidx.browser:browser:$project.androidxVersion"
    implementation "androidx.legacy:legacy-support-v4:$project.androidxVersion"
    implementation "com.google.android.material:material:$project.androidxVersion"
    implementation "androidx.cardview:cardview:$project.androidxVersion"
    implementation "androidx.preference:preference:$project.androidxVersion"
    implementation "androidx.constraintlayout:constraintlayout:$project.constraintLayoutVersion"

    //android architecture
    kapt "androidx.lifecycle:lifecycle-compiler:$project.archVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$project.archVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$project.archVersion"
    implementation "androidx.arch.core:core-testing:$project.archVersion"

    //Firebase
    implementation "com.google.firebase:firebase-core:$project.firebaseCoreVersion"
    implementation "com.google.firebase:firebase-firestore:$project.firebaseFirestoreVersion"
    implementation "com.google.firebase:firebase-storage:$project.firebaseStorageVersion"
    implementation "com.crashlytics.sdk.android:crashlytics:$project.crashlyticsVersion"
    implementation "com.firebaseui:firebase-ui-auth:$project.firebaseUIVersion"

    //Dagger core
    kapt "com.google.dagger:dagger-compiler:$project.daggerVersion"
    implementation "com.google.dagger:dagger:$project.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$project.daggerVersion"

    //Dagger Android
    kapt "com.google.dagger:dagger-android-processor:$project.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$project.daggerVersion"

    //RxKotlin
    implementation "io.reactivex.rxjava2:rxkotlin:$project.rxKotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$project.rxAndroidVersion"

    //Timber logger
    implementation "com.jakewharton.timber:timber:$project.timberVersion"

    //Google Play Services
    implementation "com.google.android.gms:play-services-base:$project.gmsBaseVersion"
    implementation "com.google.android.gms:play-services-basement:$project.gmsBasementVersion"
    implementation "com.google.android.gms:play-services-flags:$project.gmsBaseVersion"
    implementation "com.google.android.gms:play-services-tasks:$project.gmsBaseVersion"
    implementation "com.google.android.gms:play-services-auth:$project.gmsBaseVersion"
    implementation "com.google.android.gms:play-services-location:$project.gmsVersion"
    implementation "com.google.android.gms:play-services-maps:$project.gmsVersion"

    //Glide
    kapt "com.github.bumptech.glide:compiler:$project.glideVersion"
    implementation "com.github.bumptech.glide:glide:$project.glideVersion"

    //retrofit
    implementation "com.squareup.retrofit2:converter-gson:$project.retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$project.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$project.retrofitVersion"

    //okHttp
    implementation "com.squareup.okhttp3:okhttp:$project.okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$project.okHttpVersion"

    //fab menu
    implementation "com.getbase:floatingactionbutton:$project.fabMenuVersion"

    //EasyImage
    implementation "com.github.jkwiecien:EasyImage:$project.easyImageVersion"

    //Compressor
    implementation "id.zelory:compressor:$project.compressorVersion"

    //LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$project.leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$project.leakCanaryVersion"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$project.leakCanaryVersion"

    //TESTS LOCAL

    //jUnit
    testImplementation "junit:junit:$project.junitVersion"

    //Mockito
    testImplementation "org.mockito:mockito-core:$project.mockitoVersion"

    //MockitoKotlin
    testImplementation "com.nhaarman:mockito-kotlin:$project.mockitoKotlinVersion"

    //Robolectric
    testImplementation "org.robolectric:robolectric:$project.robolectricVersion"
    testImplementation "org.robolectric:shadows-support-v4:$project.robolectricShadowsVersion"
}

apply plugin: 'com.google.gms.google-services'

def getApiKey(String propertyName) {
    def propsFile = rootProject.file('apikeys.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propertyName]
    } else {
        return ""
    }
}
